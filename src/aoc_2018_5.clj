(ns aoc-2018-5
  (:require [clojure.string :as str]))

;===========[Part 1]===========

(defn reactive?
  "ë°˜ì‘í•  ìˆ˜ ìˆëŠ” ì¡°ê±´
  (reactive? a A) => true
  (reactive? A a) => true
  " [left right]
  (= 32 (Math/abs (- (int left) (int right)))))

;example : vVabcZz
;unit => remain
;v => [v]
;V => []
;a => [a]
;b => [a b]
;c => [a b c]
;Z => [a b c Z]
;z => [a b c]

(defn generate-remain-polymer
  "reactive? ì— ë¶€í•©í•˜ì§€ ì•ŠëŠ” ë¬¸ìì—´ vector(=remain)ë¥¼ ì·¨í•©"
  [remain unit]
  (if (and (seq remain) ;NullPointException ë°©ì§€
           (reactive? (peek remain) unit)) ;ğŸŒŸì„±ëŠ¥ê°œì„ !ğŸŒŸlast -> peek
    (vec (pop remain)) ;ğŸŒŸì„±ëŠ¥ê°œì„ !ğŸŒŸdrop-last -> pop
    (conj remain unit)))

(def polymer (slurp "resources/input_2018_5.txt"))

(comment
  (->> polymer
       (reduce generate-remain-polymer [])
       count))

;===========[Part 2]===========


(def units "ABCDEFGHIJKLMNOPQRSTUVWXYZ")

(defn polymer-without-unit
  "unit(ì•ŒíŒŒë²³) í•˜ë‚˜ì”© ë¹ ì§„ polymer sequence"
  [original-polymer unit]
  (str/replace original-polymer
               (re-pattern (format "(?i)[%s]" unit))
               ""))

(defn get-remain-count [str]
  (->> str
       (reduce generate-remain-polymer [])
       count))

(comment
  (->> units
       (map (partial polymer-without-unit polymer))
       (map get-remain-count)
       (apply min)))



